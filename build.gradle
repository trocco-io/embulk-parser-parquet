plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "com.diffplug.spotless" version "5.15.0"
    id "com.palantir.git-version" version "0.12.3"
}
repositories {
    mavenCentral()
}

group = "io.trocco"
description = "An embulk output plugin to egest records as json via http/https."
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.43"
    compileOnly "org.embulk:embulk-spi:0.10.49"
    compile "org.embulk:embulk-util-file:0.1.3"
    compile "org.embulk:embulk-util-json:0.1.1"
    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile "org.embulk:embulk-util-config:0.3.3"

    compile 'org.apache.parquet:parquet-avro:1.9.0'
    compile 'org.apache.parquet:parquet-hadoop:1.10.1'
    compile 'org.apache.hadoop:hadoop-mapreduce-client-core:3.3.6'
    compile 'org.apache.hadoop:hadoop-common:3.3.6'

    compile project(path: ":shadow-jackson-jq", configuration: 'shadow')

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-junit4:0.10.49"
    testCompile "org.embulk:embulk-core:0.10.49"
    testCompile "org.embulk:embulk-deps:0.10.49"
}

embulkPlugin {
    mainClass = "org.embulk.parser.parquet.ParquetParserPlugin"
    category = "parser"
    type = "parquet"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}
gem {
    from("LICENSE.txt")
    authors = [ "" ]
    email = [ "" ]
    summary = "Parquet parser plugin for Embulk."
    homepage = "https://github.com/trocco-io/embulk-parser-parquet"
    licenses = [ "MIT" ]

    into("lib/embulk/guess/") {
        from "lib/embulk/guess/parquet.rb"
    }
}
spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat().aosp()
    }
}
test {
    testLogging {
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams true
    }
}
